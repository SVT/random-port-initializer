plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id 'org.jmailen.kotlinter' version '1.20.1'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.ben-manes.versions" version '0.20.0' 
}

group = 'se.svt.oss.spring'
description = 'A Spring test util for resolving property placeholders to free random ports'
project.version=scmVersion.version
ext.vcsUrl = 'https://github.com/SVT/random-port-initializer'
ext.license = [:]
ext.license.name = "The Apache Software License, Version 2.0"
ext.license.shortName = "Apache-2.0"
ext.license.url = "http://www.apache.org/licenses/LICENSE-2.0.txt"

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply from: 'svt-publish.gradle'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//need for jcenter/maven repo compliance
artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

configurations.all {
    exclude module: 'junit'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task testJar(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
}

publish.dependsOn check

dependencies {
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.0'
    api 'org.jetbrains.kotlin:kotlin-reflect:1.3.0'
    api 'org.springframework:spring-context:5.1.2.RELEASE'

    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.springframework:spring-test:5.1.2.RELEASE'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task wrapper(type: Wrapper) {
    distributionType = 'ALL'
    gradleVersion = '4.10.2'
}
